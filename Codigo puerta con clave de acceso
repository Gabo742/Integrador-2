#include <Keypad.h>
#include <LiquidCrystal_I2C.h>
#include <Wire.h>
#include <ESP32Servo.h>

#define ROW_NUM     4 
#define COLUMN_NUM  4 

Servo servoMotor;

char keys[ROW_NUM][COLUMN_NUM] = {
  {'1','2','3', 'A'},
  {'4','5','6', 'B'},
  {'7','8','9', 'C'},
  {'*','0','#', 'D'}
};

byte pin_rows[ROW_NUM]      = {19, 18, 5, 17};
byte pin_column[COLUMN_NUM] = {16, 4, 0, 2};  

Keypad keypad = Keypad(makeKeymap(keys), pin_rows, pin_column, ROW_NUM, COLUMN_NUM );
LiquidCrystal_I2C lcd(0x27, 16, 2);

String password = "1234";
String enteredPassword = "";

enum States {
  AWAIT_INPUT,
  CHANGE_PASSWORD
};
States currentState = AWAIT_INPUT;

void setup(){
  servoMotor.attach(15);
  lcd.init(); 
  lcd.backlight();
  lcd.print("Password:");
  servoMotor.write(0);
}

void loop(){
  char key = keypad.getKey();

  if (key) {
    if (currentState == AWAIT_INPUT) {
      processEntry(key);
    } 
    else if (currentState == CHANGE_PASSWORD) {
      processPasswordChange(key);
    }
  }
}

void processEntry(char key) {
  if (key == 'A' ) { 
    if (enteredPassword == password) {
      lcd.clear();
      lcd.print("Access granted");
      servoMotor.write(180);      
      delay(2000);
      servoMotor.write(0);
    } 
    else {
      lcd.clear();
      lcd.print("Access denied");
      delay(2000);
    }
    lcd.clear();
    lcd.print("Enter Password");
    enteredPassword = "";
  } 
  else if (key == 'B' ) { 
    enteredPassword = "";
    lcd.clear();
    lcd.setCursor(0,0);    
    lcd.print("Password:");
    lcd.setCursor(0,1);
    lcd.print(" ");
  } 
  else if (key == 'C') { 
    currentState = CHANGE_PASSWORD;
    lcd.clear();
    lcd.print("New Password:");
    enteredPassword = "";
  } 
  else {
    enteredPassword += key;
    lcd.setCursor(enteredPassword.length()-1,1);
    lcd.print("*");
  }
}

void processPasswordChange(char key) {
  if (key == 'A' ) { 
    currentState = AWAIT_INPUT;
    password = enteredPassword; 
    lcd.clear();
    lcd.print("Pwd changed");
    delay(2000);
    lcd.clear();
    lcd.print("Enter Password");
    enteredPassword = "";
  } 
  else if (key == 'B' ) { 
    enteredPassword = "";
    lcd.setCursor(0,1);
    lcd.print("");
  } 
  else {
    enteredPassword += key;
    lcd.setCursor(enteredPassword.length(), 1);
    lcd.print(key);
  }
}
